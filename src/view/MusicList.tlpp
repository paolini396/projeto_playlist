#Include "TOTVS.CH"
#INCLUDE "TOPCONN.CH"
#Include 'Set.CH'

/*/{Protheus.doc} User Function MusicList
  @author Paolini
  @since 27/12/2021
  @version 1.0
  @description Tela para Buscar músicas na API VAGALUME
  @type  Function
/*/
User Function MusicList()

	Local aArea := GetArea()
	Local oLayer as object
	Local aSize as array
	Private cTitulo  := "Lista de Músicas"
	Private oDlgTela as object
	Private oBrowse as object
	Private oCbxFiltro as object
	Private cSearch := Space(200)
	Private cAliasQry := GetNextAlias()

	Private aBrwData  as array

	aSize	:= FWGetDialogSize( oMainWnd )
	oDlgtela := MsDialog():New( aSize[1], aSize[2], aSize[3], aSize[4], cTitulo,,,, nOr( WS_VISIBLE, WS_POPUP ),,,,, .T.,,,, .F. )
	oLayer := FWLayer():New()
	oLayer:Init(oDlgTela,.F.,.T.)

	//-- DIVISOR DE TELA SUPEIROR [ FILTRO ]

	oLayer:AddLine("LINESUP", 25 )
	oLayer:AddCollumn("BOX01", 100,, "LINESUP" )
	oLayer:AddWindow("BOX01", "PANEL01", "Filtros", 100, .F.,,, "LINESUP" ) //"Filtros"
	//-- DIVISOR DE TELA INFERIOR [ GRID ]
	oLayer:AddLine("LINEINF", 75 )
	oLayer:AddCollumn( "BOX02", 100,, "LINEINF" )
	oLayer:AddWindow( "BOX02", "PANEL02", cTitulo	, 100, .F.,,, "LINEINF" )

	FPanel01( oLayer:GetWinPanel( "BOX01", "PANEL01", "LINESUP" ) ) //Contrução do Painel de Filtros
	FPanel02( oLayer:GetWinPanel( "BOX02", "PANEL02", "LINEINF" ) ) //Contrução do Painel de Musicas

	oDlgtela:Activate()
	RestArea(aArea)

Return

Static Function FPanel01( oPanel )

	Local bFiltrar	:=	Nil

	//-- Inclui a borda pora apresentacao dos componentes em tela
	TGroup():New( 005, 005, (oPanel:nHeight/2) - 005, (oPanel:nWidth/2) - 010 , "", oPanel,,, .T. ) //"Filtros"

	TSay():New( 010, 250, { || "Digite o nome da Música"    }, oPanel,,,,,, .T.,,, 110, 010 )
	TGet():New( 020, 250, { |u| If(PCount()>0,cSearch := u, cSearch)},oPanel,200,010,'@!',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","cSearch",,)

	//Inclui botao filtrar
	bFiltrar := { || HandleSearch("Buscando Música") }
	TButton():New( 020,460, "Buscar", oPanel, bFiltrar, 050, 013,,,, .T. ) //"Filtrar"

Return()


Static Function FPanel02( oPanel )

	Local aBrwModel as array
	Local aBrwCol as array
	Local aBrwSeek as array

	Local nIndex as numeric

	Local bAddPlayList	:=	Nil


	aBrwModel := {}
	aBrwCol := {}
	aBrwData := {}
	aBrwSeek := {}

	aAdd(aBrwData, {"TESTE 01", "Paolini"} )
	aAdd(aBrwData, {"TESTE 02", "Paolini"} )

	aAdd(aBrwModel, {'Nome'        , '@!'    , 25, 10, 1})
	aAdd(aBrwModel, {'Banda'  , '@!'    , 25, 00, 1})

	bAddPlayList := { || HandleAddPlayList("Listando Play List") }
	TButton():New( 020,460, "Adicionar a Play List", oPanel, bAddPlayList, 050, 013,,,, .T. ) 

	for nIndex := 1 to Len(aBrwModel)

		aAdd(aBrwCol, FwBrwColumn():New())

		aBrwCol[Len(aBrwCol)]:SetData( &('{ || aBrwData[oBrowse:nAt,' + cValToChar(nIndex) + ']}') )
		aBrwCol[Len(aBrwCol)]:SetTitle(aBrwModel[nIndex,1])
		aBrwCol[Len(aBrwCol)]:SetPicture(aBrwModel[nIndex,2])
		aBrwCol[Len(aBrwCol)]:SetSize(aBrwModel[nIndex,3])
		aBrwCol[Len(aBrwCol)]:SetDecimal(aBrwModel[nIndex,4])
		aBrwCol[Len(aBrwCol)]:SetAlign(aBrwModel[nIndex,5])

	Next nIndex

	oBrowse := FwBrowse():New()
	oBrowse:SetDataArray()
	oBrowse:SetArray(aBrwData)
	oBrowse:SetColumns(aBrwCol)
	oBrowse:SetOwner(oPanel)
	oBrowse:Activate()

Return()


Static Function HandleSearch(cMensagem)

	Default cMensagem	:=  ""

	If !Empty(cMensagem)
		FWMsgRun( ,{|| UpdateBrw() },"Aguarde",cMensagem)
	Else
		CursorWait()
		UpdateBrw()
		CursorArrow()
	EndIf

Return

Static Function HandleAddPlayList(cMensagem)

	Default cMensagem	:=  ""

	If !Empty(cMensagem)
		FWMsgRun( ,{|| },"Aguarde",cMensagem)
	Else
		CursorWait()
		//UpdateBrw()
		CursorArrow()
	EndIf

Return

Static Function UpdateBrw()
	Local oMusicService as Object
	Local oMusicList as Object
	Local nIndex as numeric

	oMusicService := MusicService():new()
	oMusicList := oMusicService:FilterList(cSearch)

	If (!Empty(oMusicList["error"]) .and. !Empty(oMusicList["message"]))

		MsgAlert(oMusicList["message"],oMusicList["error"])

		Return
	EndIf

	aBrwData := {}
	for nIndex := 1 to Len(oMusicList["data"])

		aAdd(aBrwData, {oMusicList["data"][nIndex]["CTITULO"], oMusicList["data"][nIndex]["CBANDA"]})

	Next nIndex
	
	oBrowse:DeActivate(.T.)
	oBrowse:SetDataArray()
	oBrowse:SetArray(aBrwData)
	oBrowse:Activate()
	oBrowse:UpdateBrowse(.T.)
Return
